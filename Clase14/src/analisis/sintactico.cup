package analisis;

// importaciones
import java_cup.runtime.Symbol;
import java.util.LinkedList;
import java.util.HashMap;
import abstracto.Instruccion;
import simbolo.*;
import excepciones.Errores;

//INSTRUCCIONES
import instrucciones.Print;
import instrucciones.Declaracion;
import instrucciones.AsignacionVar;
import instrucciones.If;
import instrucciones.For;
import instrucciones.Metodo;
import instrucciones.Execute;
import instrucciones.Llamada;


//EXPRESIONES
import expresiones.Nativo;
import expresiones.Suma;
import expresiones.Resta;
import expresiones.NegacionUnaria;
import expresiones.Equals;
import expresiones.AccesoVar;
import expresiones.Menor;

// parser
parser code
{:
    scanner s;
    parser(scanner s){this.s = s;}
    
    public LinkedList<Errores> erroresSintacticos = new LinkedList<>();

    public void syntax_error(Symbol s){
        erroresSintacticos.add(new Errores("SINTACTICO RECUPERABLE",
        "No se esperaba el componente "+s.value,
        s.left,
        s.right));
    }

    public void unrecovered_syntax_error (Symbol s){
        erroresSintacticos.add(new Errores("SINTACTICO NO RECUPERABLE",
            "No se esperaba el componente "+s.value,
            s.left,
            s.right));
    }

:}

action code
{:
:}


terminal String CADENA, ENTERO, DECIMAL, ID;
terminal FINCADENA, IMPRIMIR;
terminal PAR1, PAR2, MAS, MENOS, UMENOS, IGUAL, EQUALS;
terminal LLAVE1, LLAVE2, MENOR, FOR;
terminal INT, DOUBLE, STRING, TRUE, FALSE, IF, BOOL, VOID;
terminal COMA, EXECUTE;

nonterminal LinkedList<Instruccion> INICIO, INSTRUCCIONES;
nonterminal Instruccion INSTRUCCION, IMPRESION, DECLARACION, ASIGNACION, SIF, CFOR, ACTUALIZA_FOR;
nonterminal Instruccion METODO;
nonterminal Instruccion EXPRESION;
nonterminal Tipo TIPOS;
nonterminal LinkedList<HashMap> PARAMS, PARAMSCALL;
nonterminal Instruccion RUN, LLAMADA;
nonterminal HashMap<String, Object> PARAM;

precedence left EQUALS, MENOR;
precedence left MAS, MENOS;
precedence right UMENOS;

start with INICIO;

INICIO ::= INSTRUCCIONES:a    {: RESULT = a; :}
;

INSTRUCCIONES ::= INSTRUCCIONES:a INSTRUCCION:b     {: RESULT = a; RESULT.add(b); :}
                | INSTRUCCION:a                     {: RESULT = new LinkedList<Instruccion>(); RESULT.add(a); :}
;

INSTRUCCION ::= IMPRESION:a FINCADENA   {: RESULT = a; :}
              | DECLARACION:a FINCADENA {: RESULT = a; :}
              | ASIGNACION:a FINCADENA  {: RESULT = a; :}
              | SIF:a                   {: RESULT = a; :} 
              | CFOR:a                  {: RESULT = a; :}
              | METODO:a                {: RESULT = a; :}
              | RUN:a FINCADENA         {: RESULT = a; :}
              | LLAMADA:a FINCADENA     {: RESULT = a; :}
              | error FINCADENA         {: RESULT = null; :}
;

IMPRESION ::= IMPRIMIR PAR1 EXPRESION:a PAR2  {: RESULT = new Print(a, aleft, aright); :}
;

DECLARACION ::= TIPOS:a ID:b IGUAL EXPRESION:c   {: RESULT = new Declaracion(b, c, a, aleft, aright); :}
;

ASIGNACION ::= ID:a IGUAL EXPRESION:c   {: RESULT = new AsignacionVar(a, c, aleft, aright); :}
;

SIF ::= IF PAR1 EXPRESION:a PAR2 LLAVE1 INSTRUCCIONES:b LLAVE2  {: RESULT = new If(a, b, aleft, aright ); :}
;

CFOR ::= FOR PAR1 ASIGNACION:a FINCADENA EXPRESION:b FINCADENA ACTUALIZA_FOR:c PAR2 LLAVE1 INSTRUCCIONES:d LLAVE2   {: RESULT = new For(a, b, c, d, aleft, aright); :}
;

ACTUALIZA_FOR ::= ASIGNACION:a  {: RESULT = a; :}
;

METODO ::= TIPOS:a ID:b PAR1 PARAMS:d PAR2 LLAVE1 INSTRUCCIONES:c LLAVE2   {: RESULT = new Metodo(b, d, c, a, aleft, aright ); :}
         | TIPOS:a ID:b PAR1 PAR2 LLAVE1 INSTRUCCIONES:c LLAVE2   {: RESULT = new Metodo(b, new LinkedList<HashMap>(), c, a, aleft, aright ); :}
; 

PARAMS ::= PARAMS:a COMA PARAM:b     {: RESULT = a;
                                   RESULT.add(b); :}
         | PARAM:b              {: RESULT = new LinkedList<HashMap>();
                                   RESULT.add(b); :}
;

PARAM ::= TIPOS:a ID:b IGUAL EXPRESION:c    {:var hm2 = new HashMap<String,Object>();
                                               hm2.put("tipo",a);
                                               hm2.put("id",b);
                                               hm2.put("expresion",c);
                                               RESULT = hm2;:}
        | TIPOS:a ID:b                      {: var hm = new HashMap<String,Object>();
                                               hm.put("tipo",a);
                                               hm.put("id",b);
                                               hm.put("expresion",null);
                                               RESULT = hm; :}
;

// public Execute(String id, LinkedList<HashMap> parametros, int linea, int col) {
RUN ::= EXECUTE ID:a PAR1 PARAMSCALL:b PAR2   {: RESULT = new Execute(a, b, aleft, aright); :}
      | EXECUTE ID:a PAR1 PAR2                {: RESULT = new Execute(a, new LinkedList<HashMap>(), aleft, aright); :}
;

LLAMADA ::= ID:a PAR1 PARAMSCALL:b PAR2         {: RESULT = new Llamada(a, b, aleft, aright); :}
          | ID:a PAR1 PAR2                      {: RESULT = new Llamada(a, new LinkedList<HashMap>(), aleft, aright); :}
;

PARAMSCALL ::= PARAMSCALL:a COMA ID:b IGUAL EXPRESION:c {: var hm2 = new HashMap<String,Object>();
                                               hm2.put("id",b);
                                               hm2.put("expresion",c);
                                               RESULT = a;
                                               RESULT.add(hm2);
:}
            | ID:b IGUAL EXPRESION:c            {:var hm = new HashMap<String,Object>();
                                               hm.put("id",b);
                                               hm.put("expresion",c);
                                               RESULT = new LinkedList<HashMap>();
                                               RESULT.add(hm);:}
;


TIPOS ::= INT       {: RESULT = new Tipo(tipoDato.ENTERO); :}
        | DOUBLE    {: RESULT = new Tipo(tipoDato.DECIMAL); :}
        | STRING    {: RESULT = new Tipo(tipoDato.CADENA); :}
        | BOOL      {: RESULT = new Tipo(tipoDato.BOOLEANO); :}
        | VOID      {: RESULT = new Tipo(tipoDato.VOID); :}
;

EXPRESION ::= MENOS EXPRESION:a             {: RESULT = new NegacionUnaria(a, aleft, aright);  :} %prec UMENOS
            | EXPRESION:a MAS EXPRESION:b   {: RESULT = new Suma(a, b, aleft, aright); :}
            | EXPRESION:a MENOS EXPRESION:b {: RESULT = new Resta(a, b, aleft, aright);  :}
            | EXPRESION:a EQUALS EXPRESION:b {: RESULT = new Equals(a, b, aleft, aright); :}
            | EXPRESION:a MENOR EXPRESION:b {: RESULT = new Menor(a, b, aleft, aright); :}
            | ENTERO:a                  {: RESULT = new Nativo(Integer.parseInt(a),new Tipo(tipoDato.ENTERO), aleft, aright); :}
            | CADENA:a                  {: RESULT = new Nativo(a,new Tipo(tipoDato.CADENA), aleft, aright); :}
            | DECIMAL:a                 {: RESULT = new Nativo(new Double(a),new Tipo(tipoDato.DECIMAL), aleft, aright); :}
            | TRUE:a                     {: RESULT = new Nativo(true,new Tipo(tipoDato.BOOLEANO), aleft, aright); :}
            | FALSE:a                    {: RESULT = new Nativo(false,new Tipo(tipoDato.BOOLEANO), aleft, aright); :}
            | ID:a                      {: RESULT = new AccesoVar(a, aleft, aright); :}
            | PAR1 EXPRESION:a PAR2     {: RESULT = a; :}
;
